/*
        prelude.an
    Standard functions and datatypes pre-imported
    into every Ante source file.
*/

//C functions
printf (format: ref c8) ... -> i32
puts (str: ref c8) -> i32
putchar (character:c8) -> unit
getchar () -> c8
exit (status:i32) -> 'never_returns
malloc (size:usz) -> ref 'a
calloc (num:usz) (size:usz) -> ref 'a
realloc (ptr: ref 'a) (size:usz) -> ref 'a
free (ptr: ref 'a) -> unit
memcpy (destination:ref 'a) (source:ref 'b) (num:usz) -> ref 'a //dest
system (command: ref c8) -> i32
strlen (str: ref c8) -> usz

//C stdio
type File = (f:ref unit)
type FilePos = (f:ref unit)

type InFile = f:File
type OutFile = f:File

fopen (filename:ref c8) (mode:ref c8) -> File
fclose (stream:File) -> unit

fputs (str: ref c8) (stream:OutFile) -> i32
fputc (character:i32) (stream:OutFile) -> i32
fgetc (stream:InFile) -> i32
fgets (str: ref c8) (num:i32) (stream:InFile) -> ref c8
ungetc (character:i32) (stream:InFile) -> i32

fgetpos (stream:File) (pos:FilePos) -> unit
ftell (stream:File) -> i64
fsetpos (stream:File) (pos:FilePos) -> unit
fseek (stream:File) (offset:i64) (origin:i32) -> i32

feof (stream:InFile) -> i32
ferror (stream:File) -> i32

//Convenience function for using fgetc, and feof with better types
fgetc8 f = cast (fgetc f) : c8
feofb f = cast (feof f) : bool

//Ante datatypes
type Str =
    cStr: (ref c8)
    len: usz

type Maybe 't =
   | Some 't
   | None

type Type 't = none:unit


trait Empty 't
    empty (Type 't) -> 't


impl Eq (Maybe 'e) given Eq 'e
    (==) l r =
        match (l, r) with
        | (Some lv, Some rv) -> lv == rv
        | (None, None) -> true
        | _ -> false

trait Cast 'a /*to*/ 'b
    cast 'a -> 'b

trait TryCast 'a 'b
    try_cast 'a -> Maybe 'b

impl TryCast 'a 'b given Cast 'a 'b
    try_cast a = Some (cast a)

impl Cast (Maybe 'm) Str given Cast 'm Str
    cast maybe =
        match maybe with
        | Some val -> "Some (" ++ cast val ++ ")"
        | None -> "None"


//Compile-time functions
module Ante
    //prints type and value (llvm dump) of variable while compiling
    ante debug (val:'t) -> unit

    //returns the size of a type in Bytes.  Does not evaluate its argument
    ante sizeof (val:'t) -> usz

    //return the type of an expression.  Note: Type is an ante type (compile-time only)
    ante typeof (val:'t) -> Type 't

    //Issue an error message during compile-time and stop compiling the current function
    ante error (msg: ref c8) -> 'never_returns

    //dumps llvm-ir to stdout
    ante emit_ir () -> unit

    //useful in the repl to redefine functions
    ante forget (name: ref c8) -> unit


trait Add 'n
    (+) 'n 'n -> 'n

trait Sub 'n
    (-) 'n 'n -> 'n

trait Mul 'n
    (*) 'n 'n -> 'n

trait Div 'n
    (/) 'n 'n -> 'n

trait Mod 'n
    (%) 'n 'n -> 'n

trait Pow 'n
    (^) 'n 'n -> 'n

trait Cmp 'n
    (<) 'n 'n -> bool
    (>) 'n 'n -> bool
    //(<=) l r = not (l > r)
    //(>=) l r = not (l < r)

trait Eq 't
    (==) 't 't -> bool
    // (!=) l r = not (l == r)

trait Is 't
    (is) 't 't -> bool

trait Append 't
    (++) 't 't -> 't

trait Extract 'col 'index
    type ExtractElem
    (#) 'col 'index -> ExtractElem

trait Insert 'col 'index 'elem
    // temporary until functions with the same name and
    // a differing number of parameters can be declared.
    insert (mut 'col) 'index 'elem -> unit

trait In 'elem 'col
    (in) 'elem 'col -> bool

trait Range 'l 'r
    type RangeResult
    (..) 'l 'r -> RangeResult

trait Deref 't
    type DerefResult
    (@) 't -> DerefResult

trait Neg 't
    neg 't -> 't

trait Not 't
    (not) 't -> 't


trait Print 't
    printne 't -> unit

print x =
    printne "${x}\n"


//numerical print functions
impl Print i8
    printne x = printf ("%hhd".cStr) x

impl Print i16
    printne x = printf ("%hd".cStr) x

impl Print i32
    printne x = printf ("%d".cStr) x

impl Print i64
    printne x = printf ("%ld".cStr) x

impl Print isz
    printne x = printf ("%zi".cStr) x

//unsigned
impl Print u8
    printne x = printf ("%hhu".cStr) x

impl Print u16
    printne x = printf ("%hu".cStr) x

impl Print u32
    printne x = printf ("%u".cStr) x

impl Print u64
    printne x = printf ("%lu".cStr) x

impl Print usz
    printne x = printf ("%zu".cStr) x

//floats
impl Print f16
    printne x = printf ("%.2f".cStr) (cast x : f64)

impl Print f32
    printne x = printf ("%f".cStr) (cast x : f64)

impl Print f64
    printne x = printf ("%.14f".cStr) x

//char
impl Print c8
    printne x = printf ("%c".cStr) x

//bool
impl Print bool
    printne b =
        if b then printf ("true".cStr)
        else printf ("false".cStr)

//c-string
impl Print (ref c8)
    printne s = printf ("%s".cStr) s


impl Print Str
    printne s = printne (s.cStr)


impl Print 't given Cast 't Str
    printne t = printne (cast t : Str)


impl Cast Str (ref c8)
    cast s = s.cStr


module Str
    reverse (s:Str) -> Str =
        buf = mut malloc (s.len + 1usz)

        i = mut 0usz
        while i < s.len do
            buf#i := s.cStr#(s.len - i - 1usz)
            i += 1usz

        buf#i := '\0'
        Str buf i


impl Cast i64 Str
    cast i =
        if i == 0i64 then return "0"
        len = mut 0usz
        alloc_size = 20usz
        buf = mut malloc (alloc_size + 1usz)
        buf#alloc_size := '\0'

        x = mut i
        isNeg = i < 0i64 and ((x *= -1i64); true)

        while x != 0i64 do
            len += 1usz
            buf#(alloc_size - len) := '0' + cast (x % 10i64)
            x /= 10i64

        if isNeg then
            len += 1usz
            buf#(alloc_size - len) := '-'

        offset = cast buf + (alloc_size - len)
        Str (cast offset) len


impl Cast u64 Str
    cast i =
        if i == 0u64 then return "0"
        len = mut 0usz
        alloc_size = 20usz
        buf = mut malloc (alloc_size + 1usz)
        buf#alloc_size := '\0'

        x = mut i
        while x != 0u64 do
            len += 1usz
            buf#(alloc_size - len) := '0' + cast (x % 10u64)
            x /= 10u64

        offset = cast buf + (alloc_size - len)
        Str (cast offset) len


// convert all other numeric types to strings by widening first
impl Cast i8 Str
    cast x = x as i64 as Str

impl Cast i16 Str
    cast x = x as i64 as Str

impl Cast i32 Str
    cast x = x as i64 as Str

impl Cast isz Str
    cast x = x as i64 as Str

impl Cast u8 Str
    cast x = x as u64 as Str

impl Cast u16 Str
    cast x = x as u64 as Str

impl Cast u32 Str
    cast x = x as u64 as Str

impl Cast usz Str
    cast x = x as u64 as Str


module U64
    parse (s:Str) -> Maybe u64 =
        i = mut 0usz
        sum = mut 0u64

        while s.cStr#i != '\0' do
            c = s.cStr#i
            if c >= '0' and c <= '9' then
                sum *= 10u64
                sum += cast (c - '0')
                i += 1usz
                None
            else
                return None

        Some sum

module I64
    parse (s:Str) -> Maybe i64 =
        i = mut 0usz
        sum = mut 0i64
        isNeg =
            if s.len == 0usz or s.cStr#0usz == '-' then false
            else ((i += 1usz); true)

        while s.cStr#i != '\0' do
            c = s.cStr#i
            if c >= '0' and c <= '9' then
                sum *= 10i64
                sum += cast (c - '0')
                i += 1usz
                None
            else
                return None

        Some (if isNeg then -1i64 * sum else sum)


impl Eq (ref 't) given Eq 't
    (==) l r = @l == @r


//For-loops work over any range that is Iterable
trait Iterable 'i
    into_iter 'i -> 'it given Iterator 'it

trait Iterator 'i
    type IteratorElem
    has_next 'i -> bool
    cur_elem 'i -> IteratorElem
    advance 'i -> 'i

type LazyRange = start:i32 end:i32 step:i32

//returns true if a is in the range r
impl In i32 LazyRange
    (in) a r =
        if r.step == 1 then
            r.start <= a and a < r.end
        else
            a_adj = a % r.step
            s_adj = r.start % r.step

            a_adj == s_adj and r.start <= a and a < r.end

//creates an end-exclusive range
impl Range i32 i32
    type RangeResult = r:LazyRange
    (..) start end =
        LazyRange start end 1

//Takes a tuple of the first two integers and calculates the step by their difference
impl Range (i32,i32) i32
    type RangeResult = f:LazyRange
    (..) first_two end =
        LazyRange (first_two#0usz) end (first_two#1usz - first_two#0usz)

impl Iterator LazyRange
    type IteratorElem = e:i32
    cur_elem r = r.start
    advance r = LazyRange (r.start + r.step) r.end r.step
    has_next r = (r.step > 0 and r.start < r.end) or (r.step < 0 and r.start > r.end)


//Iterating through an InFile iterates through each line
impl Iterator InFile
    type IteratorElem = s:Str
    has_next f = not feofb f
    cur_elem f = InFile.next_line f
    advance f = f


module OutFile
    write (f:OutFile) (s:Str) =
        fputs (s.cStr) f

module Str
    print (s:Str) =
        puts s.cStr

impl Cast (ref c8) Str
    cast cStr = Str cStr (cast (strlen cStr))

impl Eq Str
    (==) l r =
        if l.len != r.len then
            return false

        for i in 0 .. cast l.len do
            if l.cStr#i != r.cStr#i then
                return false
        true

impl Is Str
    (is) l r = l.cStr is r.cStr


impl Append Str
    (++) s1 s2 =
        if s1.len == 0usz then return s2
        if s2.len == 0usz then return s1

        len = s1.len + s2.len
        buf = mut malloc (len+1usz)

        memcpy buf (s1.cStr) s1.len

        buf_offset = cast (cast buf + s1.len)
        memcpy buf_offset (s2.cStr) (s2.len+1usz) //copy terminating null too

        Str buf len

impl Extract Str usz
    type ExtractElem = c:c8
    (#) s index = s.cStr#index


impl Insert Str usz c8
    insert str index char =
        str.cStr#index := char

impl Cast Str InFile
    cast fName = InFile (fopen fName.cStr "r".cStr)


impl Cast Str OutFile
    cast fName = OutFile (fopen fName.cStr "w".cStr)


module InFile
    next_line (f:InFile) -> Str =
        if feofb f then return ""
        eof = cast 255

        len = mut 0usz
        cap = mut 64usz
        cstr = mut malloc cap

        while
            c = fgetc8 f

            if c == eof then break

            //Flag feof if eof occurs after terminating newline
            if c == '\n' then
                peek = fgetc8 f
                if peek != eof then ungetc (cast peek) f
                break

            if len+1usz >= cap then
                cap *= 2usz
                cstr := realloc cstr cap

            cstr#len := c
            len += 1usz
            true
        do ()

        cstr#len := '\0'
        Str cstr len


//print a message then return user input
input msg -> Str =
    len = mut 0usz
    cstr = mut cast 0

    printne msg

    while
        c = getchar ()
        if len % 32usz == 0usz then
            cstr := realloc cstr (len+32usz)

        cstr#len := c
        len += 1usz
        c != '\n'
    do ()

    len -= 1usz
    cstr#len := '\0'
    Str cstr len
